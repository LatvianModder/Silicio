def version_mod = "2.0.0"

// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

/*
// for people who want stable - not yet functional for MC 1.8.8 - we require the forgegradle 2.1 snapshot
plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
}
*/

version = "1.9.4-" + version_mod
group = "com.latmod.silicio"
archivesBaseName = "Silicio"

sourceCompatibility = targetCompatibility = "1.8"
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.10.2-12.18.2.2099"
    runDir = "run"
    mappings = "snapshot_20160518"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace "@VERSION@", version_mod
    replaceIn "Silicio.java"
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'http://maven.amadornes.com' }
    maven { url "http://dvs1.progwml6.com/files/maven" }
}

dependencies {
    compile 'com.github.LatvianModder:FTBLib:3.0.1'
    deobfCompile "MCMultiPart:MCMultiPart:1.2.1:universal"
    deobfCompile "mezz.jei:jei_1.10.2:3.9.0.246:api"
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'src/main/api'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}